plugins {
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'war'
}

ext {
    file_name_build = 'violetnote-wss-build.war'
    file_name_prod = 'violetnote-wss.war'
    file_name_int = 'violetnote-int-wss.war'
}

bootWar {
    archiveFileName = file_name_build
}

processResources {
    filesMatching('gr.properties') {
        expand(project.properties)
    }
}

group = 'com.romanpulov'
version = '1.7.11'

description = """violetnote-wss Webapp"""

repositories {
    mavenCentral()
    //for violetnote-core, jcore-utils
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aspectj'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-json'

    implementation 'com.romanpulov:jutils-core:0.4.4'
    implementation 'com.romanpulov:violetnote-core:2.1.4'

    // test
    testRuntimeOnly 'org.springframework.boot:spring-boot-starter-tomcat'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-restclient'
    testImplementation 'org.springframework.boot:spring-boot-resttestclient'
 }

test {
    useJUnitPlatform()
}

apply from: '../common/gradle/tomcat.config.gradle'

tasks.register('deployInt', Copy) {
    dependsOn(bootWar)
    from "build/libs"
    into tomcat_webapps
    include file_name_build
    rename file_name_build, file_name_int
}

tasks.register('deployProd', Copy) {
    dependsOn(bootWar)
    from "build/libs"
    into tomcat_webapps
    include file_name_build
    rename file_name_build, file_name_prod
}

tasks.register('printProps') {
    doLast {
        println tomcat_webapps
    }
}
