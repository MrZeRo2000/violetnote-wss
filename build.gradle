plugins {
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'war'
}

ext {
    file_name_build = 'violetnote-wss-build.war'
    file_name_prod = 'violetnote-wss.war'
    file_name_int = 'violetnote-int-wss.war'
}

bootWar {
    archiveFileName = file_name_build
}

processResources {
    filesMatching('gr.properties') {
        expand(project.properties)
    }
}

group = 'com.romanpulov'
version = '1.6.1'
sourceCompatibility = '11'

description = """violetnote-wss Webapp"""

repositories {
    mavenCentral()
    //for violetnote-core, jcore-utils
    mavenLocal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'com.romanpulov:jutils-core:0.3.12'
    implementation 'com.romanpulov:violetnote-core:2.0.8'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

ext {
    if (project.hasProperty("tomcat_home")) {
        tomcat_webapps = tomcat_home + '/webapps'
    } else {
        tomcat_webapps = 'D:/prj/apache-tomcat-9.0.24/webapps'
    }
}

tasks.register('deployInt', Copy) {
    dependsOn(bootWar)
    from "build/libs"
    into tomcat_webapps
    include file_name_build
    rename file_name_build, file_name_int
}

tasks.register('deployProd', Copy) {
    dependsOn(bootWar)
    from "build/libs"
    into tomcat_webapps
    include file_name_build
    rename file_name_build, file_name_prod
}

tasks.register('printProps') {
    doLast {
        println tomcat_webapps
    }
}
